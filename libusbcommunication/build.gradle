apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

Properties props = new Properties()
props.load(new FileInputStream(project.rootProject.file('local.properties')))
String libusbDir = props['libusb.dir']

ext {
    PUBLISH_GROUP_ID = 'me.jahnen.libaums'
    PUBLISH_VERSION = '0.3.0'
    PUBLISH_ARTIFACT_ID = 'libusbcommunication'
}

apply from: "${rootProject.projectDir}/publish-module.gradle"

android {
    kotlinOptions {
        jvmTarget = '21'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 33
        compileSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                arguments "-DLIBUSB_PATH=" + libusbDir
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            }
            ndkVersion '25.2.9519653'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    namespace 'me.jahnen.libaums.libusbcommunication'

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'com.eclipsesource.minimal-json:minimal-json:0.9.4'
    testImplementation 'org.xenei:junit-contracts:0.1.7'
    testImplementation 'org.mockito:mockito-core:2.28.2'

    api project(':libaums')
}

